=head1 Hash

Z<hashes>

X<hash>

Un I<hash> è una struttura dati nativa del Perl che associa delle chiavi stringa a
dei valori scalari. Allo stesso modo in cui una variabile corrisponde a un'area
di memoria, una chiave di un hash si riferisce a un valore. Pensate a un hash come se
fosse una guida telefonica, con cui potete usare i nomi dei vostri amici per trovare i loro numeri di telefono.
In altri linguaggi gli hash sono detti I<tablelle>, I<array associativi>, I<dizionari>
o I<mappe>.

Gli hash hanno due proprietà importanti: memorizzano uno scalare per ogni chiave univoca e
non impongono alcun ordine specifico sulle chiavi.

=head2 Dichiarazione di Hash

Z<declaring_hashes>

X<hash; dichiarazione>
X<C<%>; sigillo>
X<sigilli; C<%>>

Gli hash usano il sigillo C<%>. Dichiarate un hash lessicale con:

=begin programlisting

    my %gusti_preferiti;

=end programlisting

Un hash appena dichiarato è vuoto. Potreste anche scrivere C<my %gusti_preferiti = ();>, ma
sarebbe superfluo.

X<C<$>; sigillo>
X<sigilli; C<$>>

Gli hash usano il sigillo C<$> quando accedete a singoli elementi e le parentesi graffe
C<{ }> per specificare la chiave:

=begin programlisting

    my %gusti_preferiti;
    $gusti_preferiti{Gabri} = 'Cioccolato';
    $gusti_preferiti{Anna}  = 'Vaniglia';

=end programlisting

È possibile assegnare una lista di chiavi e valori ad un hash in un'unica espressione:

=begin programlisting

    my %gusti_preferiti = (
        'Gabri', 'Cioccolato',
        'Anna',  'Vaniglia',
    );

=end programlisting

X<<< C<< => >>; operatore fat comma >>>
X<<< operatori; C<< => >> >>>

Se assegnate un numero dispari di elementi ad un hash, riceverete un warning.
Un idioma del Perl suggerisce di usare l'operatore I<fat comma> (C<< => >>)
per associare chiavi e valori, poiché rende più esplicite tali associazioni:

=begin programlisting

    my %gusti_preferiti = (
        Gabri B<< => >> 'Menta e cioccolato',
        Anna  B<< => >> 'Vaniglia',
    );

=end programlisting

L'operatore fat comma funziona come la virgola, ma quota anche
la bareword (L<barewords>) che lo precede. La direttiva C<strict> non genererà un warning
per la presenza di tale bareword--e se avete una funzione con lo stesso nome di una chiave
dell'hash, l'operatore fat comma I<non> chiamerà la funzione:

=begin programlisting

    sub nome { 'Leonardo' }

    my %indirizzo =
    (
        nome => 'Via Fibonacci 1123',
    );

=end programlisting

La chiave in questo hash sarà C<nome> e non C<Leonardo>. Se volete chiamare la funzione,
dovete farlo esplicitamente:

=begin programlisting

    my %indirizzo =
    (
        B<nome()> => 'Via Fibonacci 1123',
    );

=end programlisting

=begin notetip

L'assegnamento ad un hash avviene in contesto lista. Qualunque funzione chiamata durante l'assegnamento ad un
hash userà per default il contesto lista a meno che usiate esplicitamente C<scalar()>.

=end notetip

Per svuotare un hash, assegnategli la lista vuotaN<Potreste vedere occasionalmente anche C<undef
%hash>.>:

=begin programlisting

    %gusti_preferiti = ();

=end programlisting

=head2 Indicizzazione di Hash

X<hash; valori>

Per accedere ai singoli valori di un hash dovete eseguire un'operazione di indicizzazione. Usate una chiave (operazione di
accesso I<per chiave>) per estrarre un valore da un hash:

=begin programlisting

    my $indirizzo = $indirizzi{$nome};

=end programlisting

In questo esempio, C<$nome> contiene una stringa che è anche una chiave dell'hash.
Come per l'accesso a un singolo elemento di un array, il sigillo dell'hash
è cambiato da C<%> a C<$> per indicare l'accesso per chiave a un valore scalare.

Potete anche usare letterali stringa come chiavi di hash. Perl quoterà le bareword
automaticamente seguendo le stesse regole del fat comma:

=begin programlisting

    # quotato automaticamente
    my $indirizzo = $indirizzi{Vittorio};

    # quotatura necessaria; non è una bareword valida
    my $indirizzo = $indirizzi{B<'>Maria LuisaB<'>};

    # una chiamata di funzione deve essere disambiguata
    my $indirizzo = $indirizzi{dammi_un_nomeB<()>};

=end programlisting

=begin tip Non Quotatemi

I principianti tendono a quotare i letterali stringa che usano come chiavi di hash, mentre i programmatori esperti
evitano la quotatura quando è possibile. La quotatura delle chiavi di hash viene usata
per segnalare l'intenzione di fare qualcos'altro.

=end tip

Anche le funzioni native del Perl 5 sono soggette alla quotatura automatica:

=begin programlisting

    my %indirizzi =
    (
        Leonardo => 'Via Fibonacci 1123',
        Utako    => 'Hotel Cantor, Stanza 1',
    );

    sub da_nome_a_indirizzo
    {
        return $indirizzi{B<+>shift};
    }

=end programlisting

L'operatore I<più> unario (L<unary_coercions>) converte quella che sarebbe una bareword (C<shift>)
soggetta alla quotatura automatica in un'espressione. Da questo esempio si vede che potete usare una
espressione arbitraria--non solo una chiamata di funzione--come chiave di un hash:

=begin programlisting

    # non I<fatelo> davvero, per favore
    my $indirizzo = $indirizzi{reverse 'odranoeL'};

    # l'interpolazione funziona
    my $indirizzo = $indirizzi{"$nome $cognome"};

    # e anche le chiamate di metodi
    my $indirizzo = $indirizzi{ $utente->nome() };

=end programlisting

Le chiavi di un hash possono solo essere stringhe. Qualunque cosa valuti ad una stringa è
una chiave di hash ammissibile. Perl si spinge fino a fare la coercizione (L<coercion>) di
altri tipi nel tipo stringa, in modo che se usate un oggetto come chiave di hash, ottenete
la versione stringa dell'oggetto anziché l'oggetto stesso:

=begin programlisting

    for my $isbn (@lista_isbn)
    {
        my $libro = Libro->per_isbn( $isbn );

        # difficile che sia quello che volevate fare
        $libri{$libro} = $libro->prezzo;
    }

=end programlisting

=head2 Esistenza di Chiavi di Hash

X<predefinite; funzioni; C<exists>>
X<hash; C<exists>>

L'operatore C<exists> restituisce un valore booleano che indica se un hash
contiene una data chiave:

=begin programlisting

    my %indirizzi =
    (
        Leonardo => 'Via Fibonacci 1123',
        Utako    => 'Hotel Cantor, Stanza 1',
    );

    say "Ho l'indirizzo di Leonardo"
        if exists $indirizzi{Leonardo};
    say "Ho l'indirizzo di Galileo"
        if exists $indirizzi{Galileo};

=end programlisting

Usare C<exists> anziché tentare di accedere direttamente a una chiave di hash evita due problemi.
Prima di tutto, il controllo non viene effettuato sul I<valore> associato alla chiave; infatti una chiave di hash
potrebbe esistere e avere un valore anche se tale valore corrisponde a falso in contesto booleano (per esempio C<undef>):

=begin programlisting

    my  %valore_chiave_falso = ( 0 => '' );
    ok( %valore_chiave_falso,
         'un hash che contiene falso sia come chiave che come valore
          dovrebbe valutare a vero' );

=end programlisting

Inoltre, C<exists> evita l'autovivificazione (L<autovivification>) nelle strutture dati
annidate (L<nested_data_structures>).

X<predefinite; funzioni; C<defined>>

Se una chiave di hash esiste, il suo valore potrebbe essere C<undef>. Potete controllarlo con C<defined>:

=begin programlisting

    $indirizzi{Leibniz} = undef;

    say "Gottfried vive a $indirizzi{Leibniz}"
        if exists  $indirizzi{Leibniz}
        && defined $indirizzi{Leibniz};

=end programlisting

=head2 Accedere a Chiavi e Valori degli Hash

X<hash; C<keys>>
X<predefinite; funzioni; C<keys>>

Gli hash sono variabili aggregate, ma la loro struttura a coppie offre diverse
possibilità di iterazione: sulle chiavi dell'hash, sui valori dell'hash o
sulle coppie chiave/valore. L'operatore C<keys> produce una lista di chiavi di un hash:

=begin programlisting

    for my $destinatario (keys %indirizzi)
    {
        say "Trovato l'indirizzo di $destinatario!";
    }

=end programlisting

X<hash; C<values>>
X<predefinite; funzioni; C<values>>

L'operatore C<values> produce una lista di valori di un hash:

=begin programlisting

    for my $indirizzo (values %indirizzi)
    {
        say "Qualcuno vive in $indirizzo";
    }

=end programlisting

X<hash; C<each>>
X<predefinite; funzioni; C<each>>

L'operatore C<each> produce una lista di liste di due elementi contenenti una chiave e il suo valore:

=begin programlisting

    while (my ($destinatario, $indirizzo) = each %indirizzi)
    {
        say "$destinatario vive in $indirizzo";
    }

=end programlisting

Al contrario degli array, non c'è un ordinamento ovvio per queste liste. L'ordinamento
dipende dall'implementazione interna dell'hash, la versione di
Perl che state usando, la dimensione dell'hash e un ulteriore fattore random. Ciò nonostante,
l'ordine degli elementi imposto da C<keys>, C<values> e C<each> è consistente.
Modificando l'hash l'ordine potrebbe cambiare, ma se l'hash rimane immutato potete
fare affidamento su tale ordine.

Ogni hash ha un I<unico> iteratore per l'operatore C<each>. Non potete
iterare su un hash con più di un C<each> senza incorrere in problemi; se iniziate una nuova
iterazione mentre un'altra è in corso, la prima terminerà prematuramente e la seconda
inizierà da qualche parte in mezzo all'hash. Durante una iterazione C<each>, fate attenzione
a non chiamare funzioni che possano a loro volta tentare di iterare sull'hash con
C<each>.

In pratica questo succede raramente, ma quando vi dovesse servire resettate l'iteratore di un hash con C<keys> o
C<values> in contesto void:

=begin programlisting

    # resetta l'iteratore dell'hash
    keys %indirizzo;

    while (my ($destinatario, $indirizzo) = each %indirizzi)
    {
        ...
    }

=end programlisting

=head2 Slice di Hash

X<hash; slice>
X<slice; hash>

Una I<slice di hash> è una lista di chiavi o valori di un hash indicizzata in una singola
operazione. Per inizializzare diversi elementi di un hash in una sola istruzione:

=begin programlisting

    # %gatti contiene già altri elementi
    @gatti{qw( Jack Brad Mars Grumpy )} = (1) x 4;

=end programlisting

Questo codice è equivalente all'inizializzazione:

=begin programlisting

    my %gatti = map { $_ => 1 }
                qw( Jack Brad Mars Grumpy );

=end programlisting

...eccetto che l'inizializzazione di uno slice di hash non I<sostituisce> i contenuti di un hash
che esistono già.

X<C<@>; sigillo>
X<sigilli; C<@>>

Le slice di hash vi permettono anche di estrarre diversi valori da un hash in un'unica
operazione. Come per le slice di array, il sigillo dell'hash cambia per indicare il contesto
lista. L'uso delle parentesi graffe indica accesso per chiave rendendo non ambigua la 
presenza di un hash:

=begin programlisting

    my @indirizzi_compratori = @indirizzi{ @compratori };

=end programlisting

Le slice di hash rendono facile anche fondere due hash insieme:

=begin programlisting

    my %indirizzi          = ( ... );
    my %indirizzi_canadesi = ( ... );

    @indirizzi{ keys   %indirizzi_canadesi }
              = values %indirizzi_canadesi;

=end programlisting

Questo codice è equivalente a ciclare manualmente sui contenuti di C<%indirizzi_canadesi>,
ma è molto più compatto.

Che cosa succede se la stessa chiave compare in entrambi gli hash? L'uso di uno slice di hash I<sovrascrive>
sempre le coppie chiave/valore in C<%indirizzi>. Se desiderate un comportamento diverso,
è più appropriato ciclare manualmente.

=head2 L'Hash Vuoto

Un hash vuoto non contiene né chiavi né valori. In contesto booleano ha valore
falso. Un hash che contiene almeno una coppia chiave/valore ha invece valore vero in
contesto booleano anche se tutte le chiavi o tutti i valori o entrambi
avessero valore falso in contesto booleano.

=begin programlisting

    use Test::More;

    my %vuoto;
    ok( ! %vuoto, 'un hash vuoto dovrebbe avere valore falso' );

    my %chiave_falsa = ( 0 => 'valore vero' );
    ok( %chiave_falsa, 'un hash contenente falso come chiave
                        dovrebbe avere valore vero' );

    my %valore_falso = ( 'chiave vera' => 0 );
    ok( %valore_falso, 'un hash contenente falso come valore
                        dovrebbe avere valore vero' );

    done_testing();

=end programlisting

In contesto scalare, un hash valuta a una stringa che rappresenta la frazione di
posizioni occupate nell'hash--ma potete tranquillamente ignorare questi dettagli
dell'implementazione interna dell'hash.

In contesto lista, un hash valuta a una lista di coppie chiave/valore simile a quella
restituita dall'operatore C<each>.  Tuttavia, non I<potete> iterare su
tale lista allo stesso modo in cui potete iterare sulla lista prodotta da C<each>, poiché
il ciclo non terminerebbe mai:

=begin programlisting

    # ciclo infinito per hash non vuoti
    while (my ($chiave, $valore) = %hash)
    {
        ...
    }

=end programlisting

È I<possibile> ciclare su questa lista di chiavi e valori con un ciclo C<for>, ma la
variabile iteratore conterrà una chiave ad una iterazione e il suo valore in quella successiva,
poiché Perl appiattisce l'hash in un'unica lista di chiavi e valori alternati.

=head2 Idiomi degli Hash

X<hash; trovare elementi distinti>

Poiché ogni chiave può comparire una sola volta in un hash, assegnare la stessa chiave ad un hash più
di una volta memorizza solo il valore più recente. Potete sfruttare questo fatto per trovare gli elementi
distinti di una lista:

=begin programlisting

    my %unic;
    undef @unic{ @elementi };
    my @unici = keys %unic;

=end programlisting

L'uso di C<undef> con uno slice di hash setta i valori dell'hash a C<undef>. Questo idioma
rappresenta il modo più efficiente di effettuare operazioni insiemistiche con un hash.

X<hash; contare elementi>

Gli hash sono utili anche per contare degli elementi, come gli indirizzi IP in un file di log:

=begin programlisting

    my %indirizzi_ip;

    while (my $linea = <$file_di_log>)
    {
        my ($ip, $risorsa) = analizza_linea( $linea );
        $indirizzi_ip{$ip}++;
        ...
    }

=end programlisting

Inizialmente ogni valore di un hash è C<undef>. L'operatore di postincremento
(C<++>) lo tratta come se fosse zero. In questo esempio, se il valore della chiave esiste già viene rimpiazzato con
il suo incremento. Se invece non esiste alcun valore per la chiave, Perl crea un
valore (C<undef>) e lo incrementa immediatamente a uno, dato che il valore numerico di
C<undef> è 0.

X<hash; caching>
X<manovra orchescese>

La seguente strategia costituisce un utile meccanismo di caching per memorizzare il risultato di un'operazione
costosa con un piccolo overhead:

=begin programlisting

    {
        my %cache_utenti;

        sub restituisci_utente
        {
            my $id = shift;
            $cache_utenti{$id} //= crea_utente($id);
            return $cache_utenti{$id};
        }
    }

=end programlisting

X<defined-or>
X<C<//>; operatore infisso>
X<operatori; C<//>>
X<operatori; defined-or>

Questa I<manovra orchescese>N<NdT: in inglese è un gioco di parole tra orcish (manovra da orchi) e or-cache.> restituisce il valore dell'hash,
se esso esiste. Altrimenti calcola, mette in cache e restituisce il
valore. L'operatore di assegnamento defined-or (C<//=>) valuta il suo operando di sinistra.
Se tale operando non è definito, l'operatore gli assegna il valore del suo operando di destra.
In altre parole, se non c'è un valore nell'hash per una data
chiave, questa funzione chiama C<crea_utente()> passandole la chiave e poi aggiorna
l'hash.

Perl 5.10 ha introdotto gli operatori di defined-or e di assegnamento defined-or. Prima
del 5.10, gran parte del codice usava l'operatore di assegnamento boolean-or (C<||=>) per lo stesso
scopo. Purtroppo, alcuni valori validi valutano a falso in contesto booleano, quindi valutare la I<definitezza> dei valori
è quasi sempre più accurato. Essendo pigra, la manovra orchescese testa la definitezza del valore in cache, non
la sua verità.

X<hash; parametri per nome>

Se la vostra funzione accetta diversi argomenti, sfruttate lo slurping degli argomenti
(L<parameter_slurping>) per raccogliere delle coppie chiave/valore in un unico hash trattandole come
argomenti passati per nome alla funzione:

=begin programlisting

    sub prepara_coppa
    {
        my %parametri = @_;
        ...
    }

    prepara_coppa( gusto  => 'Limone',
                   glassa => 'biscotti macinati' );

=end programlisting

Questo approccio vi permette anche di settare dei valori di default, in questo modo:

=begin programlisting

    sub prepara_coppa
    {
        my %parametri         = @_;
        B<$parametri{gusto}   //= 'Vaniglia';>
        B<$parametri{glassa}  //= 'cioccolato';>
        B<$parametri{praline} //= 100;>
        ...
    }

=end programlisting

... oppure includendoli direttamente nell'inizializzazione dell'hash, dato che un assegnamento
in una certa posizione sovrascrive gli assegnamenti che lo precedono:

=begin programlisting

    sub prepara_coppa
    {
        my %parametri =
        (
            B<< gusto    => 'Vaniglia', >>
            B<< glassa   => 'cioccolato', >>
            B<< praline  => 100, >>
            @_,
        );
        ...
    }

=end programlisting

=head2 Bloccare gli Hash

Z<locked_hashes>

X<hash; bloccati>

Dato che le chiavi di hash sono delle bareword, offrono poca protezione contro gli errori di scrittura rispetto alla
protezione offerta dalla direttiva C<strict> sui nomi di funzione e di variabile. Il
poco conosciuto modulo core C<Hash::Util> fornisce dei meccanismi per migliorare la situazione.

Per prevenire l'inserimento accidentale di una chiave di hash indesiderata
(a causa di una svista o di input utente inaffidabile), usate la funzione C<lock_keys()>
per congelare l'hash al suo insieme corrente di chiavi. Ogni tentativo di aggiungere una
nuova chiave all'hash solleverà un'eccezione. Questa è ovviamente una sicurezza debole, adatta solo a
prevenire errori accidentali; chiunque può usare la funzione C<unlock_keys()> per rimuovere
questa protezione.

In modo simile potete bloccare o sbloccare il valore corrente di una data chiave dell'hash
(C<lock_value()> e C<unlock_value()>) e impostare o rilasciare il modo read-only per l'intero hash
con C<lock_hash()> e C<unlock_hash()>.
